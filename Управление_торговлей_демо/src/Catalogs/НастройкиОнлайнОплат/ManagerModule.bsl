///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Функция получения юридических данных эквайера Яндекс.Кассы на дату запроса.
//
// Параметры:
//  ДатаСреза  - Дата - дата на которую требуется получить юр. данные эквайера.
//
// Возвращаемое значение:
//   Структура  - Данные эквайера, содержащие следующие реквизиты:
//    * ПолноеНаименование
//    * Наименование
//    * ИНН
//    * КПП
//    * ОГРН
//    * ОКВЕД
//    * ОКПО
//
Функция ДанныеЭквайераПоУмолчанию(ДатаСреза) Экспорт 
	
	ЭквайерыXML = Справочники.НастройкиОнлайнОплат.ПолучитьМакет("Эквайеры").ПолучитьТекст();
	ЭквайерыXMLТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(ЭквайерыXML).Данные;
	
	ЭквайерыXMLТаблица.Колонки.Добавить("ДатаСреза", Новый ОписаниеТипов("Дата",,,
													 Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	Для Каждого СтрокаТаблицы Из ЭквайерыXMLТаблица Цикл 
		СтрокаТаблицы.ДатаСреза = ?(ПустаяСтрока(СтрокаТаблицы.DateFrom), Дата('00010101'), Дата(СтрокаТаблицы.DateFrom));
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ЭквайерыXMLТаблица"	, ЭквайерыXMLТаблица);
	Запрос.УстановитьПараметр("ДатаСреза"			, ДатаСреза);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЭквайерыXMLТаблица.FullName,
	               |	ЭквайерыXMLТаблица.Name,
	               |	ЭквайерыXMLТаблица.INN,
	               |	ЭквайерыXMLТаблица.KPP,
	               |	ЭквайерыXMLТаблица.OGRN,
	               |	ЭквайерыXMLТаблица.OKVED,
	               |	ЭквайерыXMLТаблица.OKPO,
	               |	ЭквайерыXMLТаблица.ДатаСреза
	               |ПОМЕСТИТЬ ЭквайерыXMLТаблица
	               |ИЗ
	               |	&ЭквайерыXMLТаблица КАК ЭквайерыXMLТаблица
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МАКСИМУМ(ЭквайерыXMLТаблица.ДатаСреза) КАК ДатаСреза
	               |ПОМЕСТИТЬ ПолучениеПоДатеСреза
	               |ИЗ
	               |	ЭквайерыXMLТаблица КАК ЭквайерыXMLТаблица
	               |ГДЕ
	               |	ЭквайерыXMLТаблица.ДатаСреза <= &ДатаСреза
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЭквайерыXMLТаблица.FullName КАК НаименованиеПолное,
	               |	ЭквайерыXMLТаблица.Name КАК Наименование,
	               |	ЭквайерыXMLТаблица.INN КАК ИНН,
	               |	ЭквайерыXMLТаблица.KPP КАК КПП,
	               |	ЭквайерыXMLТаблица.OGRN КАК ОГРН,
	               |	ЭквайерыXMLТаблица.OKVED КАК ОКВЕД,
	               |	ЭквайерыXMLТаблица.OKPO КАК КодПоОКПО
	               |ИЗ
	               |	ЭквайерыXMLТаблица КАК ЭквайерыXMLТаблица
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПолучениеПоДатеСреза КАК ПолучениеПоДатеСреза
	               |		ПО ЭквайерыXMLТаблица.ДатаСреза = ПолучениеПоДатеСреза.ДатаСреза";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ДанныеЭквайера = Новый Структура("НаименованиеПолное, Наименование, ИНН, КПП, ОГРН, ОКВЕД, КодПоОКПО");
	ЗаполнитьЗначенияСвойств(ДанныеЭквайера, Выборка);
	
	Возврат ДанныеЭквайера;
	
КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// См. ОнлайнОплаты.СписокДоступныхОрганизаций.
//
Функция СписокДоступныхОрганизаций(ТолькоСДоговором = Неопределено, ТолькоДействительные = Истина) Экспорт
	
	СписокОрганизаций = Новый Массив;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	НастройкиОнлайнОплат.Организация КАК Организация
	|ИЗ
	|	Справочник.НастройкиОнлайнОплат КАК НастройкиОнлайнОплат
	|ГДЕ
	|	НЕ НастройкиОнлайнОплат.ПометкаУдаления
	|	И ВЫБОР
	|			КОГДА &ТолькоДействительные
	|				ТОГДА НЕ НастройкиОнлайнОплат.Недействительна
	|		КОНЕЦ
	|	И НастройкиОнлайнОплат.СДоговором = &ТолькоСДоговором");
	
	Запрос.УстановитьПараметр("ТолькоДействительные", ТолькоДействительные);
	Если ЗначениеЗаполнено(ТолькоСДоговором) Тогда
		Запрос.УстановитьПараметр("ТолькоСДоговором", ТолькоСДоговором);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НастройкиОнлайнОплат.СДоговором = &ТолькоСДоговором", "ИСТИНА");
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокОрганизаций.Добавить(Выборка.Организация);
	КонецЦикла;
	
	Возврат СписокОрганизаций;
	
КонецФункции

// См. ОнлайнОплаты.НайтиНастройку.
//
Функция НайтиНастройку(КлючиПоиска, ТолькоДействительные = Истина) Экспорт 
	
	Если Не ТипЗнч(КлючиПоиска) = Тип("Структура")
		И Не ТипЗнч(КлючиПоиска) = Тип("ФиксированнаяСтруктура")
		И Не ТипЗнч(КлючиПоиска) = Тип("Соответствие")
		И Не ТипЗнч(КлючиПоиска) = Тип("ФиксированноеСоответствие") Тогда 
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	НастройкиОнлайнОплат.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиОнлайнОплат КАК НастройкиОнлайнОплат
	|ГДЕ
	|	НЕ НастройкиОнлайнОплат.ПометкаУдаления 
	|	И ВЫБОР
	|			КОГДА &ТолькоДействительные
	|				ТОГДА НастройкиОнлайнОплат.Недействительна = ЛОЖЬ
	|		КОНЕЦ
	|	#Условия#";
	
	Запрос.УстановитьПараметр("ТолькоДействительные", ТолькоДействительные);
	
	ТекстУсловия = "";
	Для Каждого КлючПоиска Из КлючиПоиска Цикл  
		
		ТекстУсловия = ТекстУсловия
			+ Символы.ПС + "И "
			+ "НастройкиОнлайнОплат." + КлючПоиска.Ключ + " = &" +КлючПоиска.Ключ;
			
		Запрос.УстановитьПараметр(КлючПоиска.Ключ, КлючПоиска.Значение);
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Условия#", ТекстУсловия);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.Ссылка;
	Иначе
		Возврат ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли