
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает дату последнего успешного обмена для указанной настройки
//
// Параметры:
//  НастройкаОнлайнОплат  - СправочникСсылка.НастройкиОнлайнОплат - Настройка Яндекс.Кассы для которой необходимо
//                                                                  вернуть дату последнего успешного обмена.
//
// Возвращаемое значение:
//   Дата   -  дата последнего успешного обмена, если успешных обменов не было, возвращает пустую дату.
//
Функция ДатаПоследнегоУспешногоОбмена(НастройкаОнлайнОплат) Экспорт 

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НастройкаОнлайнОплат", НастройкаОнлайнОплат);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СтатусыОнлайнОплат.ДатаПоследнегоУспешногоОбмена КАК ДатаПоследнегоУспешногоОбмена
	               |ИЗ
	               |	РегистрСведений.СтатусыОнлайнОплат КАК СтатусыОнлайнОплат
	               |ГДЕ
	               |	СтатусыОнлайнОплат.НастройкаОнлайнОплат = &НастройкаОнлайнОплат";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.ДатаПоследнегоУспешногоОбмена;
	КонецЕсли;
	
	Возврат Дата('00010101');
	
КонецФункции

// Функция возвращает дату последнего успешного обмена для указанной организации
//
// Параметры:
//  Организация  - СправочникСсылка.Организации - Организация для которой необходимо вернуть дату последнего успешного обмена.
//
// Возвращаемое значение:
//   Дата   -  дата последнего успешного обмена, если успешных обменов не было, возвращает пустую дату.
//
Функция ДатаПоследнегоУспешногоОбменаПоОрганизации(Организация) Экспорт 

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СтатусыОнлайнОплат.ДатаПоследнегоУспешногоОбмена КАК ДатаПоследнегоУспешногоОбмена
	               |ИЗ
	               |	РегистрСведений.СтатусыОнлайнОплат КАК СтатусыОнлайнОплат
	               |ГДЕ
	               |	СтатусыОнлайнОплат.НастройкаОнлайнОплат.Организация = &Организация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.ДатаПоследнегоУспешногоОбмена;
	КонецЕсли;
	
	Возврат Дата('00010101');
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Обновляет дату последнего успешного обмена с сервисом Яндекс.Касса
//
// Параметры:
//  НастройкаОнлайнОплат  - СправочникСсылка.НастройкиОнлайнОплат - Настройка Яндекс.Кассы которой надо обновить дату
//                                                                  последнего успешного обмена
//  ДатаУспешногоОбмена  - Дата - Дата (состав "Дата и время") последнего успешного обмена.
//
Процедура ОбновитьСтатус(НастройкаЯндексКассы, ДатаУспешногоОбмена) Экспорт 

	МенеджерЗаписи = РегистрыСведений.СтатусыОнлайнОплат.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ДанныеЗаписи(НастройкаЯндексКассы));
	МенеджерЗаписи.ДатаПоследнегоУспешногоОбмена = ДатаУспешногоОбмена;
	МенеджерЗаписи.Записать(Истина);

КонецПроцедуры

// Обновляет значение реквизита Организация для статуса обмена для указанной настройки.
//
// Параметры:
//  НастройкаОнлайнОплат  - СправочникСсылка.НастройкиОнлайнОплат - Настройка Яндекс.Кассы которой надо обновить дату
//                                                                  последнего успешного обмена.
//  Организация - СправочникСсылка.Организации - Значение которое необходимо записать в реквизит организация.
//
Процедура ОбновитьЗначениеРеквизитаОрганизация (НастройкаЯндексКассы, Организация) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.СтатусыОнлайнОплат.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ДанныеЗаписи(НастройкаЯндексКассы));
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеЗаписи(НастройкаОнлайнОплаты)
	
	ДанныеЗаписи = РегистрыСведений.СтатусыОнлайнОплат.СоздатьМенеджерЗаписи();
	ДанныеЗаписи.НастройкаОнлайнОплаты = НастройкаОнлайнОплаты;
	ДанныеЗаписи.Прочитать();
	
	Если Не ДанныеЗаписи.Выбран() Тогда 
		
		ЗначенияКлюча = Новый Структура();
		ЗначенияКлюча.Вставить("НастройкаОнлайнОплаты", НастройкаОнлайнОплаты);
		ЗначенияКлюча.Вставить("Организация", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			НастройкаОнлайнОплаты, "Организация"));
		ЗначенияКлюча.Вставить("ДатаПоследнегоУспешногоОбмена", ТекущаяДатаСеанса());
		
		ДанныеЗаписи = РегистрыСведений.СтатусыОнлайнОплат.СоздатьКлючЗаписи(ЗначенияКлюча);
		
	КонецЕсли;
	
	Возврат ДанныеЗаписи;
	
КонецФункции

#КонецОбласти

#КонецЕсли