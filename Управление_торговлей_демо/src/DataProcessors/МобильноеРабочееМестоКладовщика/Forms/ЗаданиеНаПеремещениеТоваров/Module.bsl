
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Ссылка") Тогда
		Ссылка = Параметры.Ссылка;
	Иначе
		Склад = Параметры.Склад;
		Помещение = Параметры.Помещение;
	КонецЕсли;
	
	ЗаполнитьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Описание = Новый ОписаниеОповещения("ОбновитьФорму", ЭтаФорма);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", Элемент.ТекущиеДанные.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", Элемент.ТекущиеДанные.Характеристика);
	ПараметрыФормы.Вставить("Серия", Элемент.ТекущиеДанные.Серия);
	ПараметрыФормы.Вставить("Упаковка", Элемент.ТекущиеДанные.Упаковка);
	ПараметрыФормы.Вставить("Количество", Элемент.ТекущиеДанные.КоличествоУпаковокФакт);
	ПараметрыФормы.Вставить("Режим", "Пересчет");
	ПараметрыФормы.Вставить("НомерСтроки", Элемент.ТекущаяСтрока);
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	ПараметрыФормы.Вставить("Ячейка", Элемент.ТекущиеДанные.Ячейка);

	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.ФормаПеремещенийПоЯчейкам",ПараметрыФормы,
	ЭтаФорма,,,,Описание,
	РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьКонтроль(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Сканировать(Команда)
	
	Описание = Новый ОписаниеОповещения("РезультатПоискаЯчейки", ЭтаФорма);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипПоиска", "ПоискЯчейки");
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	
	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.СканированиеШтрихкода",ПараметрыФормы,
	ЭтаФорма,,,,Описание,
	РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьФорму()
	
	СтруктураДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Склад, Комментарий, Помещение, Исполнитель");
	
	Склад       = СтруктураДокумента.Склад;
	Комментарий = СтруктураДокумента.Комментарий;
	Помещение   = СтруктураДокумента.Помещение;
	Исполнитель = СтруктураДокумента.Исполнитель;
	
	Для Каждого Строка Из Ссылка.ТоварыОтбор Цикл
		
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.ЯчейкаОтбор = Строка.Ячейка;
		НоваяСтрока.УпаковкаОтбор = Строка.Упаковка;
		НоваяСтрока.КоличествоОтбор = Строка.Количество;
		НоваяСтрока.КоличествоУпаковокОтбор = Строка.КоличествоУпаковок;
		
		// Поиск строк в размещении
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Номенклатура",   Строка.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", Строка.Характеристика);
		СтруктураПоиска.Вставить("Серия",          Строка.Серия);
		СтруктураПоиска.Вставить("Назначение",     Строка.Назначение);
		
		РезультатПоиска = Ссылка.ТоварыРазмещение.НайтиСтроки(СтруктураПоиска);
		Если РезультатПоиска.Количество() > 0 Тогда
			НоваяСтрока.ЯчейкаРазмещение             = РезультатПоиска[0].Ячейка;
			НоваяСтрока.УпаковкаРазмещение           = РезультатПоиска[0].Упаковка;
			НоваяСтрока.КоличествоРазмещение         = РезультатПоиска[0].Количество;
			НоваяСтрока.КоличествоУпаковокРазмещение = РезультатПоиска[0].КоличествоУпаковок;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФорму(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьФормуНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьФормуНаСервере(Результат)
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Номенклатура", Результат.Номенклатура);
	СтруктураПоиска.Вставить("Характеристика", Результат.Характеристика);
	СтруктураПоиска.Вставить("Серия", Результат.Серия);
	СтруктураПоиска.Вставить("Упаковка", Результат.Упаковка);
	СтруктураПоиска.Вставить("Назначение", Результат.Назначение);
	СтруктураПоиска.Вставить("Ячейка", Результат.Ячейка);
	
	КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Результат.Упаковка, Результат.Номенклатура);
	
	РезультатПоиска = Товары.НайтиСтроки(СтруктураПоиска);
	Если РезультатПоиска.Количество() > 0 Тогда
		
		РезультатПоиска[0].КоличествоУпаковокФакт = Результат.КоличествоУпаковок;
		РезультатПоиска[0].КоличествоФакт = РезультатПоиска[0].КоличествоУпаковокФакт * КоэффициентУпаковки;
		
		РезультатПоиска[0].КоличествоУпаковокРазница = РезультатПоиска[0].КоличествоУпаковок - РезультатПоиска[0].КоличествоУпаковокФакт;
		РезультатПоиска[0].КоличествоРазница = РезультатПоиска[0].КоличествоУпаковокРазница * КоэффициентУпаковки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаИнформация"] Тогда
		
		СтандартнаяОбработка = Ложь;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаКПоступлению"];
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
