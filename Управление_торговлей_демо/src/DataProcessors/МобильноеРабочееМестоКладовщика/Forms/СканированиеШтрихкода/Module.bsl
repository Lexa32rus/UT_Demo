
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипПоиска = Параметры.ТипПоиска;
	Если Параметры.Свойство("Склад") Тогда
		Склад = Параметры.Склад;
	КонецЕсли;
	
	Если Параметры.Свойство("Помещение") Тогда
		Помещение = Параметры.Помещение;
	КонецЕсли;
	
	УстановитьЗаголовок();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		
		Если ИмяСобытия = "ScanData" И МенеджерОборудованияУТКлиент.ЕстьНеобработанноеСобытие() Тогда
			
			// Преобразуем предварительно к ожидаемому формату
			Если Параметр[1] = Неопределено Тогда
				Штрихкод = Параметр[0];
			Иначе
				Штрихкод = Параметр[1][1];
			КонецЕсли;
			
			ОбработатьШтрихкод();
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НайтиПоШтрихкоду(Команда)
	
	ОбработатьШтрихкод();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЗаголовок()
	
	Если ТипПоиска = "ПоискЯчейки" Тогда
		Элементы.Заголовок.Заголовок = НСтр("ru = 'Отсканируйте штрихкод ячейки'");
	ИначеЕсли ТипПоиска = "ПоискНоменклатуры" Тогда
		Элементы.Заголовок.Заголовок = НСтр("ru = 'Отсканируйте штрихкод номенклатуры'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НайтиЯчейкуСервер(Склад, Помещение, Штрихкод)
	
	Возврат Обработки.МобильноеРабочееМестоКладовщика.НайтиЯчейкуСервер(Склад, Помещение, Штрихкод);
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиТоварСервер(Штрихкод)
	
	Возврат Обработки.МобильноеРабочееМестоКладовщика.НайтиТоварПоШК(Штрихкод);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьШтрихкод()
	
	Если ТипПоиска = "ПоискЯчейки" Тогда
		
		РезультатПоиска = НайтиЯчейкуСервер(Склад, Помещение, Штрихкод);
		
		Если ЗначениеЗаполнено(РезультатПоиска.Ячейка) Тогда
			Закрыть(РезультатПоиска.Ячейка);
		Иначе
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = НСтр("ru = 'Ячейка не найдена'");
			СообщениеПользователю.Сообщить();
		КонецЕсли;
		
	ИначеЕсли ТипПоиска = "ПоискНоменклатуры" Тогда
		
		СтруктураТовара = НайтиТоварСервер(Штрихкод);
		
		Если ЗначениеЗаполнено(СтруктураТовара.Номенклатура) Тогда
			Закрыть(СтруктураТовара);
		Иначе
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = НСтр("ru = 'Товар не найден'");
			СообщениеПользователю.Сообщить();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти