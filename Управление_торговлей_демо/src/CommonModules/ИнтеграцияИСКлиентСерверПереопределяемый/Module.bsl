
#Область ПрограммныйИнтерфейс

#Область СерииНоменклатуры

// Для некоторых видов документов может быль более одного набора параметров указания серий. 
// Нужно явно указать одну из них.
//
// Параметры:
//  ПараметрыУказанияСерий - Структура - изменяемое(возвращаемое) значение
//  Контекст - ФормаКлиентскогоПриложения
Процедура ПриОпределенииПараметровУказанияСерий(ПараметрыУказанияСерий, Контекст) Экспорт
	
	//++ НЕ ГОСИС
	Если Контекст.ИмяФормы = "Документ.РасходныйОрдерНаТовары.Форма.ФормаДокумента" Тогда
		ПараметрыУказанияСерий = Контекст.ПараметрыУказанияСерий.ОтгружаемыеТовары;
	ИначеЕсли Контекст.ИмяФормы = "Документ.ОтборРазмещениеТоваров.Форма.ФормаДокумента" Тогда
		ПараметрыУказанияСерий = Контекст.ПараметрыУказанияСерий.Отбор;
	КонецЕсли;
	//-- НЕ ГОСИС
	Возврат;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область СерииНоменклатуры

// Процедура устанавливает использование статусов серий необязательного вида "Можно указать"
// для объектов метаданных подсистемы ГосИС
//
// Параметры:
//  Использовать - Булево - разрешить использование статусов
//  ПараметрыУказанияСерий - Произвольный - Параметры указания серий.
Процедура ИспользоватьСтатусСерийМожноУказать(Использовать, ПараметрыУказанияСерий) Экспорт
	
	//++ НЕ ГОСИС
	// (См. НоменклатураКлиентСервер.ПараметрыУказанияСерий)
	Если ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ВходящаяТранспортнаяОперацияВЕТИС"
		Или ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ЗапросСкладскогоЖурналаВЕТИС"
		Или ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ИнвентаризацияПродукцииВЕТИС"
		Или ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ИсходящаяТранспортнаяОперацияВЕТИС"
		Или ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ПроизводственнаяОперацияВЕТИС"
		Или (СтрНачинаетсяС(ПараметрыУказанияСерий.ПолноеИмяОбъекта, "Обработка.ПроверкаИПодборПродукцииИСМП")
			Или СтрНачинаетсяС(ПараметрыУказанияСерий.ПолноеИмяОбъекта, "Обработка.ВиртуальнаяАгрегацияУпаковокИСМП")
			Или СтрНачинаетсяС(ПараметрыУказанияСерий.ПолноеИмяОбъекта, "Обработка.ПроверкаИПодборТабачнойПродукцииМОТП"))
			И ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено
		Тогда
		Использовать = Истина;
	КонецЕсли;
	//-- НЕ ГОСИС
	Возврат;

КонецПроцедуры

// Функция проверяет необходимость указания серии в строке по статусу.
//
// Параметры:
//  ТребуетсяУказать    - Булево - в этом статусе требуется указание серий.
//  СтатусУказанияСерий - Число  - статус указания серии в строке табличной части
//
Процедура ПроверитьНеобходимоУказатьСерию(ТребуетсяУказать, СтатусУказанияСерий) Экспорт
	
	//++ НЕ ГОСИС
	ТребуетсяУказать = НоменклатураКлиентСервер.СтатусыСерийСерияНеУказана().Найти(СтатусУказанияСерий)<>Неопределено;
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// В процедуре требуется определить признак использования серий по параметрам указания серий
//
// Параметры:
//  Использование - Булево - Признак использования серий
//  ПараметрыУказанияСерий - Произвольный - Параметры указания серий объекта конфигурации.
//
Процедура ИспользованиеСерийПоПараметрамУказанияСерий(Использование, ПараметрыУказанияСерий) Экспорт
	
	//++ НЕ ГОСИС
	Если ПараметрыУказанияСерий=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.Свойство("ИспользоватьСерииНоменклатуры") Тогда
		Использование = ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры;
	ИначеЕсли ПараметрыУказанияСерий.Свойство("ВыходныеИзделия") Тогда
		Использование = ПараметрыУказанияСерий.ВыходныеИзделия.ИспользоватьСерииНоменклатуры;
	КонецЕсли;
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

#КонецОбласти

// Заполняет представление строки номенклатуры.
//
// Параметры:
//  Представление  - Строка                                     - представление для заполнения,
//  Номенклатура   - ОпределяемыйТип.Номенклатура               - ссылка на номенклатуру,
//  Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - ссылка на характеристику номенклатуры,
//  Упаковка       - ОпределяемыйТип.Упаковка                   - ссылка на упаковку.
//  Серия          - ОпределяемыйТип.СерияНоменклатуры          - ссылка на серию номенклатуры.
Процедура ЗаполнитьПредставлениеНоменклатуры(Представление, Номенклатура, Характеристика, Упаковка, Серия) Экспорт
	
	//++ НЕ ГОСИС
	Представление = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
		СокрЛП(Номенклатура),
		СокрЛП(Характеристика),
		Упаковка,
		Серия);
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

//Описание: Корректирует имена табличных частей с данными в зависимости от источника.
//Параметры:
//	ПараметрыУказанияСерий - Структура - анализируемые ключи (их наличие обязательно):
//	*ИмяТЧТовары - Строка
//	*ИмяТЧСерии- Строка
//	*ПолноеИмяОбъекта - Строка
Процедура ПараметрыУказанияСерийЗаменитьИмяТЧ(ПараметрыУказанияСерий) Экспорт
	
	//++ НЕ ГОСИС
	Если ТипЗнч(ПараметрыУказанияСерий) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПараметрыУказанияСерий.Свойство("ПолноеИмяОбъекта")
		ИЛИ НЕ ПараметрыУказанияСерий.Свойство("ИмяТЧТовары")
		ИЛИ НЕ ПараметрыУказанияСерий.Свойство("ИмяТЧСерии") Тогда
		Возврат;
	КонецЕсли;
	
	ИмяДокументаОбъекта = ИнтеграцияИСМПУТКлиентСервер.ИмяДокументаИзПолногоИмениОбъекта(ПараметрыУказанияСерий["ПолноеИмяОбъекта"]); 
	Если ИмяДокументаОбъекта = "РасходныйОрдерНаТовары" Тогда
		ПараметрыУказанияСерий.ИмяТЧТовары = "ОтгружаемыеТовары";
		ПараметрыУказанияСерий.ИмяТЧСерии = "ОтгружаемыеТовары";
	ИначеЕсли ИмяДокументаОбъекта = "ОтборРазмещениеТоваров" Тогда
		ПараметрыУказанияСерий.ИмяТЧТовары = "ТоварыОтбор";
		ПараметрыУказанияСерий.ИмяТЧСерии = "ТоварыОтбор";
	КонецЕсли;
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

#КонецОбласти